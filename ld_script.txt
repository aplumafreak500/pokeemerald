ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 0;

SECTIONS {
    . = 0x2000000;

    ewram (NOLOAD) : ALIGN(4) {
        gHeap = .;

        . = 0x1C000;

		*(SORT(ewram_data));

        . = 0x40000;
	}

    . = 0x3000000;

    iwram (NOLOAD) : ALIGN(4) {
        /* .bss starts at 0x3000000 */
		*(SORT(iwram_data) SORT(.bss*) SORT(COMMON));
        end = .;
        . = 0x8000;
    }

    . = 0x8000000;

    .text : ALIGN(4) {
        src/crt0.o(.text);
        src/main.o(.text);
        gflib/*(SORT(.text));
        *(SORT(.text*));
    }=0

    script_data : ALIGN(4) {
        *(SORT(script_data));
    }=0

    .rodata : ALIGN(4) {
        src/main.o(.rodata);
        gflib/*(SORT(.rodata));
        *(SORT(.rodata*) SORT(.data*));
    } =0

    .ARM.exidx     : { *(.ARM.exidx) }
    .ARM.extab     : { *(.ARM.extab) }

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    .comment          0 : { *(.comment) }
    .ARM.attributes   0 : { *(.ARM.attributes) }

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
